/*
  A module to import into flakes based on flake-parts.
  Makes integration into a flake easy and tidy.
  See https://flake.parts,
*/

{ lib
, flake-parts-lib
, ...
}:
let
  clib = import ../../lib { inherit lib; };
  inherit (lib)
    mkOption
    types
    ;
in
{
  options = {
    perSystem = flake-parts-lib.mkPerSystemOption (
      { config
      , options
      , pkgs
      , ...
      }:
      {
        options = {
          mcpix = {
            settings = mkOption {
              type = types.submodule {
                options = {
                  rules = clib.types.rules;
                  servers = clib.types.servers;
                  targets = mkOption {
                    type = clib.types.flakePartsTargetOptions;
                    description = "Configuration per target";
                  };

                };

              };
              default = { };
              description = ''
                The mcpix configuration.
              '';
            };
            devShell = mkOption {
              type = types.package;
              description = "A development shell that sets up files generated by mcpix.";
              readOnly = true;
            };
            rulesFile = mkOption {
              type = lib.types.nullOr lib.types.package;
              default = null;
              readOnly = true;
            };
          };
        };
        imports = [
          # TODO: this location is a bit strange for this
          ./clients
          ./script.nix
          ./rules.nix
        ];
      }
    );
  };
}
